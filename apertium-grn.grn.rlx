OPTIONS += no-inline-sets ;

SUBREADINGS = LTR ;

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

SETS

LIST N = n ;

LIST Incp = incp ;

LIST Guess = guess ;

LIST Prn = prn ;

LIST Pos = pos ;

LIST Itg = itg ;

LIST Pers = pers ;

LIST Ind = ind ;

LIST Neg = neg ;

LIST Dat = dat ;

LIST Acc = acc ;

LIST Coop = coop ;

LIST Imp = imp;

LIST A = "a" ;

LIST Re = "re" ;

LIST Num = num ;

LIST Sg = sg ;

LIST Pl = pl ;

LIST Subs = subs ;

LIST Top = top ;

LIST Cog = cog ;

LIST Ant = ant ;

LIST Det = det ;

LIST CC = cnjcoo ;

LIST V = v ;

LIST Tv = tv ;

LIST Iv = iv ;

LIST Aux = aux ;

LIST Coop = coop ;

LIST Pass = pass ;

LIST Adj = adj ;

LIST Pro = pro ;
LIST Dem = dem ;

LIST P3 = p3 ;

LIST P1 = p1 ;

LIST Post = post ;

LIST Adv = adv;

LIST In = in ;

LIST Ac = ac ;

LIST Barb = barb ;

#Temporal Aspect

LIST Pmp = pmp;

LIST Pres = pres ;

LIST Ser = vbser ;

LIST Ref = ref ;


#Syntax

LIST @cc = @cc ;
LIST @conj = @conj ;
LIST @compound = @compound;
LIST @iobj = @iobj;
LIST @dobj = @dobj;
LIST @ccomp = @ccomp;
LIST @amod = @amod;
LIST @advmod = @advmod ;
LIST @nsubj = @nsubj;
LIST @acl = @acl;
LIST @advcl = @advcl ;

#Word forms

LIST Lang = "ñeʼẽ" ;
LIST Peoples = "guarani" "guaraní" ;
LIST Petei = "peteĩ" ;
LIST Think = "moʼã";
LIST Child = "ñemoña";
LIST PostPe = "pe" ;
LIST PostGua = "gua" ;
LIST PostGuara = "guarã";
LIST Va = "va";
LIST Ha = "ha";
LIST Jeʼe = "jeʼe";

LIST Ñandejára = "<ñandejára>"i ;

SET AntTopCog = Ant | Top | Cog ; # Subject Sg Without N
SET Names = AntTopCog | N ;
SET Subject = Names | Pers ;
SET SubjObj = Names | Prn + Dat | Prn + Acc ;
SET Verbal = V | Aux ;
SET P1P3 = P1 | P3 ;
SET Je3p = Pass | Ref ;
SET Modifiers = Adj | Pos | Num | Ind | Adj + Dem ;
SET PostModifiers = Adj | Pos;
SET VerbalPostModifiers = Va | Aux | Ha ;

#######SUFFIXES#######

LIST NomFut = "rã";
LIST NomPast = "kue" ;
LIST RoV = "rõ";

SET PostV = RoV ;
SET NomTemp = NomPast | NomFut ;
SET CompGroupDObj = Pers | Post | Adj | Tv ; 
SET PostGrnDatAcc = PostPe | PostGua ;
SET PostGrnDatAccInd = PostPe | PostGuara ;
SET TvSpeechV = Tv | Jeʼe ;

#HEURISTICS

REMOVE SUB:3 Incp ;

REMOVE SUB:2 Incp ;

REMOVE SUB:1 Incp ;

REMOVE Incp ;

REMOVE A + Prn (0/* N) ;

REMOVE Re + Prn (0/* N) ;

REMOVE Guess ;

SELECT Prn + Ind IF (1 N) ;
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT Child IF (-1/* Ant);

REMOVE Top IF ((1 Cog) OR (1 Ant)) ;
# León Cadogan omoĩ o kuatiañe'ẽme

SELECT Post IF ((-1 Top) OR (-2 Top)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe

#GRAMMATICAL

SELECT N IF (-1 Petei);
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT CC IF ((-1 N LINK 2 N) OR (-1 Names LINK 2 Names));
#Avañe'ẽ ha Guarani ñe'ẽ

SELECT Det + Pl IF (-1 N) ;
# óga kuéra

SELECT Adj + Dem + Pl IF (1/* Det + Pl) ;
# umi Guaranikuéra

REMOVE Adv IF (0/* Post);
#oje'e hese opívo oikoha
#REMOVE N IF (0/* Post);
#oje'e hese opívo oikoha

#SELECT Adv IF (1/* V) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

SELECT In IF (-1/* Adv);
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

REMOVE Sg IF ((-2/* Pl) OR (-1/* Pl)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

#SELECT Ref IF (-1/* V + Sg) ;
#oñemohendáva in
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava,
#oñemohendáva irundy tetãnguéra iñambuévape (Paraguái, Argentina, Volívia ha Vrasil)

SELECT Ser IF ((-1 N LINK 2 Num)) ;

######################################
#NOUN WITH AN ATTRIBUTE CONSTRUCTIONS#
######################################

SELECT Num IF (1/* N) ;
#mokõi óga

SELECT Adj + Ind IF (1/* N) ;
#ambue tetã


SELECT SUB:* Prn + Dem IF (1/1 Adj + Dem LINK 1/* N) ;
#kóva ko távape

SELECT Prn + Pers IF (1/* N LINK 1/* Adj);
#nde mitã michĩ

#########################
## Predicative Phrases ##
#########################

SELECT SUB:* Ser IF ((-1/* Names LINK 2/* N) OR (0/* Neg + Ser LINK -1/* Names LINK 2/* N) OR (1/* N));
#Mario ha’e chokokue
#Mario ndaha’ei chokokue
#Ndaha’ei cherembireko

REMOVE SUB:* N IF (0/* Ac);
#tou omba'aposéva

#REMOVE Adj IF ((-1/* V) OR (0/* V));

SELECT Adv + Neg IF (1/* Verbal) ;
#ani rehecha

SELECT Prn + Pos IF ((-1/* Prn + Itg) OR (0/* NomTemp)) ;
#Mávapa ipaino
#chemba'aporã

SELECT SUB:* In IF ((0/* In + N LINK 0/* Adv) OR (-1/* Ser)) ;
#Ndaha’ei cherembireko

SELECT N IF (-1/* Ser);

SELECT Dat IF ((-1/* V + Iv) OR (1/* V + Tv)) ;
#Ndahakúi ichupekuéra


##################################
#########COMPLEMENTS##############
##################################

MAP @amod TARGET Modifiers IF ((1/* N) OR (1/* Modifiers));
SELECT N IF (-1/* @amod) (NOT 0 CC) ; # risky for "ha": itúva ha isy togueraha chupe táva ruvicha kuéra oporombojovakehápe.
SELECT Pos IF (0/* @amod);
SELECT @amod IF ((1/* N) OR (1/* @amod)) ;
#pe kuña péva
SELECT Adj + Dem IF (-1/* N LINK -1* @amod);

#ko órape
#kyse pyahu FIXME: unresolved
#kóva ko távape

#Map nominal compounds

MAP @compound TARGET N IF (1 N) ; 
MAP @conj TARGET N IF (-1* N - @compound LINK 1* CC) ; 
MAP @cc TARGET CC IF (1* N + @conj BARRIER (*) - N) ; 
SELECT N IF (-1/* @compound);
#mitã jagua

SELECT SUB:* Coop IF (-1* @conj BARRIER (*) - N) ;
#Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái


SELECT Sub:* Tv IF (0/* Coop);

REMOVE SUB:* Acc IF (1/* @dobj);
#ainupã jaguápe
#Remove accusative if the following is the direct obj (already has Pe)

SELECT SUB:* PostGrnDatAcc IF (-1/* Tv LINK 1/* @dobj);
#ainupã jaguápe

REMOVE SUB:* Acc + P3 IF (0/* Ac); 
#"kuatia aiporúva" ?????

#For pronouns
MAP @iobj TARGET SubjObj IF ((-1/* @dobj LINK 1/* Dat LINK 1* PostGrnDatAccInd) OR (0/* Dat LINK 1/* @dobj));
#For NPs
MAP @iobj TARGET SubjObj IF (-1*/* Tv LINK 1*/* @dobj);
MAP @iobj TARGET SubjObj IF (1/* PostGrnDatAccInd);
#amba'apo che rúpe guarã
SELECT SUB:* Dat IF ((0/* @iobj LINK 1/* PostGrnDatAccInd) OR (0/* @iobj LINK 1/* @dobj));
#agueru yva ndéve guarã

SELECT SUB:* Dat + PostGrnDatAcc IF (-1/* @iobj);
SELECT SUB:* Names + PostGrnDatAcc IF (-1/* @iobj);

MAP @nsubj TARGET Subject IF (1*/* V BARRIER (*) - PostModifiers LINK NOT -1*/* TvSpeechV);
SELECT @nsubj IF (0/* @nsubj) (NOT 0 CC) ; # risky for 'ha': itúva ha isy togueraha chupe táva ruvicha kuéra oporombojovakehápe.
#mba'asy rereko va'ekue

#Map verbal complements

#Map verbal complement as direct object
MAP SUB:1 @ccomp TARGET V IF (-1*/* TvSpeechV LINK 1/* @nsubj); 
MAP @dobj TARGET SubjObj IF (NOT 0/* V LINK -1*/* Tv BARRIER @ccomp) ;
#ahayhu che sýpe

SELECT Post IF (-1/* TvSpeechV LINK NOT 2C/* Modifiers);
#oje'e hese opívo oikoha
MAP SUB:1 @ccomp TARGET V IF (-1*/* TvSpeechV BARRIER @nsubj LINK 1*/* V + VerbalPostModifiers BARRIER (*) - Modifiers);




SELECT N IF (0/* NomTemp);
#Che mba'aporã

REMOVE SUB:* Post IF (0/* NomTemp);
#matúlakue

SELECT V IF (0/* PostV) ;

SELECT Aux IF (-1/* V) ;
#"rereko va'ekue"

##########################
# Objective constructions#
##########################



#WORD FORMS

REMOVE Barb IF (0/* Top); 

SELECT N IF (0 Lang) (-1C Peoples) ;
# Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái (...)

SELECT SUB:* Iv IF (0/* Think LINK NOT -1/* N) ;
#Ndéve nemboguevírõ niko oimo’ãne

SELECT SUB:* N IF (0 Ñandejára) ;

AFTER-SECTIONS

SELECT SUB:0 Pres IF (0/* Pres) (0C V) ;
SELECT SUB:1 Pres IF (0/* Pres) (NOT 0/* N) ; 
  ## Oikuaa niko hikuái Ñandejára Jesucristoha
SELECT SUB:2 Pres IF (0/* Pres) ; 
SELECT SUB:3 Pres IF (0/* Pres) ;

# Tupã oporombojovakeha árape, peẽ peñenupã pohyivéta umi táva Tiro ha Sidón peguágui.


