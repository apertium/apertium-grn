OPTIONS += no-inline-sets ;

SUBREADINGS = LTR ;

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

SETS

LIST N = n ;

LIST Incp = incp ;

LIST Guess = guess ;

LIST Prn = prn ;

LIST Pos = pos ;

LIST Itg = itg ;

LIST Personal = pers ;

LIST Ind = ind ;

LIST Neg = neg ;

LIST Dat = dat ;

LIST Acc = acc ;

LIST Coop = coop ;

LIST Imp = imp;

LIST A = "a" ;

LIST Re = "re" ;

LIST Num = num ;

LIST Sg = sg ;

LIST Pl = pl ;

LIST Np = np ;

LIST Subs = subs ;

LIST Top = top ;

LIST Cog = cog ;

LIST Ant = ant ;

LIST Det = det ;

LIST Cnjcoo = cnjcoo ;

LIST V = v ;

LIST Tv = tv ;

LIST Iv = iv ;

LIST Aux = aux ;

LIST Coop = coop ;

LIST Pass = pass ;

LIST Adj = adj ;

LIST Pro = pro ;
LIST Dem = dem ;

LIST P3 = p3 ;

LIST P1 = p1 ;

LIST Post = post ;

LIST Adv = adv;

LIST In = in ;

LIST Ac = ac ;

LIST Barb = barb ;

#Temporal Aspect

LIST Pmp = pmp;

LIST Pres = pres ;

LIST Ser = vbser ;

LIST Ref = ref ;


#Syntax

LIST @cc = @cc ;
LIST @conj = @conj ;
LIST @compound = @compound;
LIST @iobj = @iobj;
LIST @dobj = @dobj;
LIST @ccomp = @ccomp;
LIST @amod = @amod;

#Word forms

LIST Lang = "ñeʼẽ" ;
LIST Peoples = "guarani" "guaraní" ;
LIST Petei = "peteĩ" ;
LIST Think = "moʼã";
LIST Child = "ñemoña";
LIST PostPe = "pe" ;
LIST PostGua = "gua" ;

SET SubSg = Ant | Top | Cog ; # Subject Sg Without N
SET Names = SubSg | N ;
SET Verbal = V | Aux ;
SET Pers = P1 | P3 ;
SET Je3p = Pass | Ref ;
SET Modifiers = Adj | Pos | Num | Ind ;

#######SUFFIXES#######

LIST NomFut = "rã";
LIST NomPast = "kue" ;
LIST RoV = "rõ";

SET PostV = RoV ;
SET NomTemp = NomPast | NomFut ;
SET CompGroupDObj = Pers | Post | Adj | Tv ; 
SET PostGrnDatAcc = PostPe | PostGua ;

#HEURISTICS

REMOVE SUB:3 Incp ;

REMOVE SUB:2 Incp ;

REMOVE SUB:1 Incp ;

REMOVE Incp ;

REMOVE A + Prn (0/* N) ;

REMOVE Re + Prn (0/* N) ;

REMOVE Guess ;

SELECT Prn + Ind IF (1 N) ;
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT Child IF (-1/* Ant);

REMOVE Top IF ((1 Cog) OR (1 Ant)) ;
# León Cadogan omoĩ o kuatiañe'ẽme

SELECT Post IF ((-1 Top) OR (-2 Top)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe

#GRAMMATICAL

SELECT N IF (-1 Petei);
#Oiko peteĩ karai Benjamín ñemoñaregua hérava Quis

SELECT Cnjcoo IF ((-1 N LINK 2 N) OR (-1 Np LINK 2 Np));
#Avañe'ẽ ha Guarani ñe'ẽ

SELECT Det + Pl IF (-1 N) ;
# óga kuéra

SELECT Adj + Dem + Pl IF (1/* Det + Pl) ;
# umi Guaranikuéra

REMOVE Adv IF (0/* Post);
#oje'e hese opívo oikoha
#REMOVE N IF (0/* Post);
#oje'e hese opívo oikoha

SELECT Adv IF (1/* V) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

SELECT In IF (-1/* Adv);
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

REMOVE Sg IF ((-2/* Pl) OR (-1/* Pl)) ;
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava

#SELECT Ref IF (-1/* V + Sg) ;
#oñemohendáva in
#Guarani ha'e peteĩva umi te'yikuéra Amérika-gua ñe'ẽnguéra apytépe hetave iñe'ẽhárava,
#oñemohendáva irundy tetãnguéra iñambuévape (Paraguái, Argentina, Volívia ha Vrasil)

SELECT Ser IF ((-1 N LINK 2 Num)) ;

######################################
#NOUN WITH AN ATTRIBUTE CONSTRUCTIONS#
######################################

SELECT Adj + Dem IF ((1/* N) OR (-1/* N LINK -1/* Adj + Dem));
#ko órape
#pe kuña péva

SELECT Num IF (1/* N) ;
#mokõi óga

SELECT Adj + Ind IF (1/* N) ;
#ambue tetã


#REMOVE Adj IF (0/* Adj + Dem LINK -2/* Adj + Dem) ; Do we need this?

SELECT Adj IF (-1/* N);
SELECT N IF ((1/* Adj) OR (-1/* Adj + Dem));
#kyse pyahu
#kóva ko távape

SELECT SUB:* Prn + Dem IF (1/1 Adj + Dem LINK 1/* N) ;
#kóva ko távape

SELECT Prn + Personal IF (1/* N LINK 1/* Adj);
#nde mitã michĩ

#########################
## Predicative Phrases ##
#########################

SELECT SUB:* Ser IF ((-1/* Names LINK 2/* N) OR (0/* Neg + Ser LINK -1/* Names LINK 2/* N) OR (1/* N));
#Mario ha’e chokokue
#Mario ndaha’ei chokokue
#Ndaha’ei cherembireko

REMOVE SUB:* N IF (0/* Ac);
#tou omba'aposéva

#REMOVE Adj IF ((-1/* V) OR (0/* V));

SELECT Adv + Neg IF (1/* Verbal) ;
#ani rehecha

SELECT Prn + Pos IF ((-1/* Prn + Itg) OR (0/* NomTemp)) ;
#Mávapa ipaino
#chemba'aporã

SELECT SUB:* In IF ((0/* In + N LINK 0/* Adv) OR (-1/* Ser)) ;
#Ndaha’ei cherembireko

SELECT N IF (-1/* Ser);

SELECT Dat IF ((-1/* V + Iv) OR (1/* V + Tv)) ;
#Ndahakúi ichupekuéra


##################################
#########COMPLEMENTS##############
##################################

MAP @amod TARGET Modifiers IF (1/* N);
SELECT N IF (-1/* @amod);
SELECT Pos IF (0/* @amod);

MAP @compound TARGET N IF (1 N) ; 
MAP @conj TARGET N IF (-1* N - @compound LINK 1* Cnjcoo) ; 

MAP @cc TARGET Cnjcoo IF (1* N + @conj BARRIER (*) - N) ; 

SELECT SUB:* Coop IF (-1* @conj BARRIER (*) - N) ;
#Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái

SELECT N IF (-1/* @compound);
#mitã jagua

SELECT Sub:* Tv IF (0/* Coop);

##Map direct objects
#MAP @iobj TARGET ObjCase IF (-1/* Tv);
#MAP @iobj TARGET Personal IF (-1/* Tv);
#MAP SUB:1 @ccomp TARGET Tv IF(1/* @iobj) ;
#SELECT SUB:* @iobj IF (-1/* @ccomp);
#SELECT SUB:* @ccomp IF (1* @iobj BARRIER (*) - ObjPos);

MAP @dobj TARGET N IF (-1*/* Tv BARRIER @ccomp) ;
#ahayhu che sýpe

REMOVE SUB:* Acc IF (1/* @dobj);
#ainupã jaguápe
#Remove accusative if the following is the direct obj (already has Pe)
SELECT SUB:* @dobj IF (-1/* Tv);
SELECT SUB:* PostGrnDatAcc IF (-1/* Tv LINK 1/* @dobj);

SELECT N IF (0/* NomTemp);
#Che mba'aporã

REMOVE SUB:* Post IF (0/* NomTemp);
#matúlakue

SELECT V IF (0/* PostV) ;

SELECT Aux IF (-1/* V) ;
#"rereko va'ekue"

##########################
# Objective constructions#
##########################



#WORD FORMS

REMOVE Barb IF (0/* Top); 

SELECT N IF (0 Lang) (-1C Peoples) ;
# Avañe'ẽ ha Guarani ñe'ẽ ombohéra hikuái (...)

SELECT SUB:* Iv IF (0/* Think LINK NOT -1/* N) ;
#Ndéve nemboguevírõ niko oimo’ãne

AFTER-SECTIONS

SELECT SUB:* Pres IF (0/* Pres) ;